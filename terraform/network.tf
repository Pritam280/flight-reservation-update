resource "aws_vpc" "main" {
    cidr_block = var.vpc_cidr
    enable_dns_support = true
    enable_dns_hostnames = true

    tags = {
        Name = "flight-vpc"
    }
  
}

resource "aws_internet_gateway" "igw" {
    vpc_id = aws_vpc.main.id

    tags = {
        Name= "flight-igw"
    }
  
}

resource "aws_subnet" "public" {
    count = length(var.public_cidrs)
    vpc_id = aws_vpc.main.id
    cidr_block = var.public_cidrs[count.index]
    availability_zone = var.azs[count.index]
    map_public_ip_on_launch = true

    tags = {
        Name = "flight-public_subnet-${count.index + 1}"
        
        "kubernetes.io/cluster/flight-eks-cluster" = "shared"
        "kubernetes.io/role/elb"                      = "1"
    }
}

resource "aws_eip" "nat" {
    domain = "vpc"
}

resource "aws_nat_gateway" "nat" {
    allocation_id = aws_eip.nat.id
    subnet_id = aws_subnet.public[0].id
    depends_on = [
                 aws_internet_gateway.igw
                ]

    tags = {
        Name = "nat-gateway"
    }
}

resource "aws_subnet" "private" {
    count = length(var.private_cidrs)
    vpc_id = aws_vpc.main.id
    cidr_block = var.private_cidrs[count.index]
    availability_zone = var.azs[count.index]
    
    tags = {
        Name = "flight-private-subnet-${count.index + 1}"

        "kubernetes.io/cluster/flight-eks-cluster" = "shared"
        "kubernetes.io/role/internal-elb"             = "1"
    }
  
}

#route table association

resource "aws_route_table" "public" {
    vpc_id = aws_vpc.main.id

    route {
        cidr_block = "0.0.0.0/0"
        gateway_id = aws_internet_gateway.igw.id
    }

    tags ={
        Name = "flight-public-rt"
    }
  
}

resource "aws_route_table" "private" {
    vpc_id = aws_vpc.main.id

    route {
        cidr_block = "0.0.0.0/0"
        nat_gateway_id = aws_nat_gateway.nat.id
    }
    tags = {
       Name = "flight-private-rt"
    }  
}

resource "aws_route_table_association" "public" {
    count = length(var.public_cidrs)
    subnet_id = aws_subnet.public[count.index].id
    route_table_id = aws_route_table.public.id
  
}

resource "aws_route_table_association" "private" {
    count = length(var.private_cidrs)
    subnet_id = aws_subnet.private[count.index].id
    route_table_id = aws_route_table.private.id

  
}